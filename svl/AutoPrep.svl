function StructurePreparation;
function Protonate3D;
function MM;

local function RemoveACE residue
  local atoms = (rAtoms residue)(1);
  local bonds = cat aBonds atoms;
  local term = bonds | aResidue bonds <> residue;
  oDestroy residue;
  Add_H term;
  return term;
endfunction

local function RemoveNME residue
  local term = RemoveACE residue;

  local bonds = (aBonds term)(1);
  local added = bonds | aElement bonds == 'H';
  aSetElement [added, 'O'];
  aSetName [added, 'OXT'];
  Add_H added;
endfunction

global function RemoveCap residues
  local residue;
  for residue in residues loop
    if rName residue == 'ACE' then
      RemoveACE residue;
    elseif rName residue == 'NME' then
      RemoveNME residue;
    endif
  endloop
endfunction

local function aNeighborHeavyAtoms atoms
  return app add (app aElement aBonds atoms <> 'H');
endfunction

local function NeutralizeOXT atoms
  local target = atoms | andE[aName atoms == 'OXT', rType aResidue atoms == 'amino'];
  aSetIon [target, 0];
  Add_H target;
endfunction

local function NeutralizeNXT atoms
  local target = atoms | andE[aBackbone atoms, aElement atoms == 'N', aNeighborHeavyAtoms atoms == 1];
  aSetIon [target, 0];
  Add_H target;
endfunction

global function AutoPrep options
  local atoms = Atoms[];

  local constraintAtoms = aAtomicNumber atoms <> 1;

  if options.tether === [] then
    aSetFixed [atoms, constraintAtoms];
  else
    local a;
    for a in atoms | constraintAtoms loop
      aSetTether [a, aPos a, options.tether, 0, 0.25];
    endloop
  endif

  local sprOptions = [batch_protonate3D: 0];
  if not isnull options.reportName then
    sprOptions.reportName = options.reportName;
  endif

  StructurePreparation sprOptions;

  atoms = Atoms[];
  Protonate3D [atoms, atoms, atoms, [], [], [rulefile: '$HOME/.moe-config/dat/protonate3d-no-HIP.dat']];

  RemoveCap Residues[];

  NeutralizeOXT Atoms[];
  NeutralizeNXT Atoms[];

  MM [cg_maxit: 10000];
endfunction
